---
layout: "project-euler-post"
problem_number: 29
problem_name: "Distinct Powers"
date: 2025-12-21
difficulty: 5
benchmark_file: "problem-0029"
benchmark_key: "count_distinct_powers_100"
---

> Consider all integer combinations of $a^b$ for $2 \le a \le 5$ and $2 \le b \le 5$:
>
> ```math
> \begin{array}{rrrr}
> 2^2=4, &2^3=8, &2^4=16, &2^5=32\\
> 3^2=9, &3^3=27, &3^4=81, &3^5=243\\
> 4^2=16, &4^3=64, &4^4=256, &4^5=1024\\
> 5^2=25, &5^3=125, &5^4=625, &5^5=3125
> \end{array}
> ```
>
> If they are then placed in numerical order, with any repeats removed, we get the following sequence of $15$ distinct terms:
>
> $$4, 8, 9, 16, 25, 27, 32, 64, 81, 125, 243, 256, 625, 1024, 3125.$$
>
> How many distinct terms are in the sequence generated by $a^b$ for $2 \le a \le 100$ and $2 \le b \le 100$?

Computing $a^b$ directly would require arbitrary precision integers since $100^{100}$ has 201 digits. This is definitely doable with `BigInt` but will slow down when we have tons of huge integers.

Instead we can use logarithms since $a^b = c^d \Leftrightarrow b \log a = d \log c$. So we compute $b \log a$ for all $(a, b)$ pairs, sort them, and count how many distinct values we get.

```julia
function count_distinct_powers(a_min, a_max, b_min, b_max; similarity_threshold=1e-10)
    powers = Float64[]
    sizehint!(powers, (a_max - a_min + 1) * (b_max - b_min + 1))

    for a in a_min:a_max
        loga = log(a)
        for b in b_min:b_max
            push!(powers, b * loga)
        end
    end

    sort!(powers)

    count = 1
    for i in 2:length(powers)
        if powers[i] - powers[i-1] > similarity_threshold
            count += 1
        end
    end

    return count
end
```

Using a tolerance of $10^{-10}$ handles floating-point precision while being small enough to distinguish different powers. The answer is computed in @benchmark[problem-0029:count_distinct_powers_100].

For extended bounds the logarithm approach works well. We can count 977,358 distinct powers for $2 \le a, b \le 1000$ in @benchmark[problem-0029:count_distinct_powers_1k] and 99,347,601 distinct powers for $2 \le a, b \le 10000$ in @benchmark[problem-0029:count_distinct_powers_10k].
