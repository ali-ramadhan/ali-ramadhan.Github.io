---
layout: "project-euler-post"
problem_number: 12
problem_name: "Highly Divisible Triangular Number"
date: 2025-12-06
---

> The sequence of triangle numbers is generated by adding the natural numbers. So the $7$<sup>th</sup> triangle number would be $1 + 2 + 3 + 4 + 5 + 6 + 7 = 28$. The first ten terms would be:
>
> $$1, 3, 6, 10, 15, 21, 28, 36, 45, 55, \dots$$
>
> Let us list the factors of the first seven triangle numbers:
> ```math
> \begin{align}
> \mathbf 1 &\colon 1\\
> \mathbf 3 &\colon 1,3\\
> \mathbf 6 &\colon 1,2,3,6\\
> \mathbf{10} &\colon 1,2,5,10\\
> \mathbf{15} &\colon 1,3,5,15\\
> \mathbf{21} &\colon 1,3,7,21\\
> \mathbf{28} &\colon 1,2,4,7,14,28
> \end{align}
> ```
>
> We can see that $28$ is the first triangle number to have over five divisors.
>
> What is the value of the first triangle number to have over five hundred divisors?

First we need a decent method of counting the number of divisors an integer $n$ has. We can do this by testing if each integer from 1 to n is a divisor. But we don't have to test each integer. If $i$ is a divisor of $n$ then $n/i$ is also a divisor of $n$. So we can just search from 1 to $\operatorname{isqrt}(n)$.

```julia
function get_divisors(n)
    divisors = Int[]

    for i in 1:isqrt(n)
        if n % i == 0
            push!(divisors, i)
            if i^2 != n  # Avoid duplicate for perfect squares
                push!(divisors, n รท i)
            end
        end
    end

    return sort(divisors)
end
```

The $n^\text{th}$ triangle number can be computed as

```math
T_n = \frac{n(n+1)}{2}
```

and we can call `get_divisors` on each triangle number until we find one with enough divisors, but $T_n$ grows quadratically so this can take a while.

Instead we can rely on the fact that if two integers $a$ and $b$ are [coprime](https://en.wikipedia.org/wiki/Coprime_integers), that is $\operatorname{gcd}(a, b) = 1$, then the number of divisors of their product equals the product of their divisor counts: $d(ab) = d(a) \times d(b)$.

It is also useful to know that [consecutive integers are always coprime](https://math.stackexchange.com/questions/2046362/consecutive-numbers-are-coprime).

To get coprime integers out of $T_n$ we notice that $n$ and $n+1$ must be coprime because they are consecutive integers. Now, one of them must be even. Dividing an even number by 2 doesn't introduce any common factors with the other number since the odd number has no factor of 2 to begin with.

So when $n$ is even, $n/2$ and $n+1$ are coprime, and the number of divisors of $T_n$ is the product of the number of divisors of $n/2$ and $n+1$. Similarly, when $n$ is odd, $n$ and $(n+1)/2$ are coprime. So in both cases we can find the number of divisors of much smaller numbers and multiply them.

We can write

```math
T_n =
\begin{cases}
    \frac{n}{2} \cdot (n+1), & \text{if $n$ is even} \\
    n \cdot \frac{n+1}{2},   & \text{if $n$ is odd}
\end{cases}
```

and implement a solution as

```julia
function find_first_triangle_with_divisors(min_divisors)
    n = 1

    while true
        if n % 2 == 0
            # T(n) = (n/2)*(n+1) if n is even
            a = n รท 2
            b = n + 1
        else
            # T(n) = n*(n+1)/2 if n is odd
            a = n
            b = (n + 1) รท 2
        end

        num_divisors = length(get_divisors(a)) * length(get_divisors(b))

        if num_divisors > min_divisors
            return n, triangle_number(n)
        end

        n += 1
    end
end
```

This lets us compute the first triangle number to have over 500 divisors in @benchmark[problem-0012:divisors_500].

It also lets us compute $T_{41040} = 842161320$ as the first triangle number to have over 1000 divisors in @benchmark[problem-0012:divisors_1000], and $T_{313599} = 49172323200$ as the first triangle to have over 2000 divisors in @benchmark[problem-0012:divisors_2000].
